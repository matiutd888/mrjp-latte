token UIdent (letter (letter | digit | '_')*) ;

-- programs ------------------------------------------------

entrypoints Program ;

ProgramDef.   Program ::= [TopDef] ;

FnDef.	   TopDef ::= Type UIdent "(" [Arg] ")" Block ;

ClassDef.   TopDef ::= "class" UIdent "{" [ClassAtr] [ClassFun] "}" ;

separator UIdent ",";
separator ClassAtr "";
separator ClassFun "";

ClassAtrT. ClassAtr ::= Type [UIdent] ";";
ClassFunT. ClassFun ::= Type UIdent "(" [Arg] ")" Block ;

separator nonempty TopDef "" ;

ArgT. 	   Arg ::= Type UIdent;

separator  Arg "," ;

-- statements ----------------------------------------------

BlockT.     Block ::= "{" [Stmt] "}" ;

separator  Stmt "" ;

Empty.     Stmt ::= ";" ;

BStmt.     Stmt ::= Block ;

Decl.      Stmt ::= Type [Item] ";" ;

NoInit.    Item ::= UIdent ; 

Init.      Item ::= UIdent "=" Expr ;

separator nonempty Item "," ;

Ass.       Stmt ::= Expr "=" Expr  ";" ;

Incr.      Stmt ::= UIdent "++"  ";" ;

Decr.      Stmt ::= UIdent "--"  ";" ;

Ret.       Stmt ::= "return" Expr ";" ;

VRet.      Stmt ::= "return" ";" ;

Cond.      Stmt ::= "if" "(" Expr ")" Stmt  ;

CondElse.  Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt  ;

While.     Stmt ::= "while" "(" Expr ")" Stmt ;

SExp.      Stmt ::= Expr  ";" ;

-- Types ---------------------------------------------------

Int.       Type ::= "int" ;

Str.       Type ::= "string" ;

Bool.      Type ::= "boolean" ;

Void.      Type ::= "void" ;

UIdentT.      Type ::= UIdent ;

internal   Fun. Type ::= Type "(" [Type] ")" ;

separator  Type "," ;

-- Expressions ---------------------------------------------

ENull.      Expr6 ::= "null";

ECast.      Expr6 ::= "(" UIdent ")" Expr6;

EObjectAttr. Expr6 ::= Expr6 "." UIdent ;

EObjectFun. Expr6 ::= Expr6 "." UIdent "(" [Expr] ")" ;

EObjectInit. Expr6 ::= "new" UIdent ;

EVar.      Expr6 ::= UIdent ;

ELitInt.   Expr6 ::= Integer ;

ELitTrue.  Expr6 ::= "true" ;

ELitFalse. Expr6 ::= "false" ;

EApp.      Expr6 ::= UIdent "(" [Expr] ")" ;


EString.   Expr6 ::= String ;

Neg.       Expr5 ::= "-" Expr6 ;

Not.       Expr5 ::= "!" Expr6 ;

EMul.      Expr4 ::= Expr4 MulOp Expr5 ;

EAdd.      Expr3 ::= Expr3 AddOp Expr4 ;

ERel.      Expr2 ::= Expr2 RelOp Expr3 ;

EAnd.      Expr1 ::= Expr2 "&&" Expr1 ;

EOr.       Expr ::= Expr1 "||" Expr ;

coercions  Expr 6 ;

separator  Expr "," ;

-- operators -----------------------------------------------

Plus.      AddOp ::= "+" ;

Minus.     AddOp ::= "-" ;

Times.     MulOp ::= "*" ;

Div.       MulOp ::= "/" ;

Mod.       MulOp ::= "%" ;

LTH.       RelOp ::= "<" ;

LE.        RelOp ::= "<=" ;

GTH.       RelOp ::= ">" ;

GE.        RelOp ::= ">=" ;

EQU.       RelOp ::= "==" ;

NE.        RelOp ::= "!=" ;

-- comments ------------------------------------------------

comment    "#" ;

comment    "//" ;

comment    "/*" "*/" ;
